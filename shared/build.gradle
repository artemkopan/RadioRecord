apply plugin: 'org.jetbrains.kotlin.multiplatform'
apply plugin: 'kotlinx-serialization'
apply plugin: 'com.squareup.sqldelight'
apply plugin: 'com.android.library'
apply plugin: 'kotlin-android-extensions'


// It is necessary because we need to have access to context on CommonCode to use SQLDelight database
android {
    compileSdkVersion 29
    defaultConfig {
        minSdkVersion 23
        targetSdkVersion 29
        versionCode 1
        versionName '1.0'
    }
    buildTypes {
        release {
            minifyEnabled false
        }
    }

    androidExtensions {
        experimental = true
    }

    // By default the android gradle plugin expects to find the kotlin source files in
    // the folder `main` and the test in the folder `test`. This is to be able place
    // the source code files inside androidMain and androidTest folders
    sourceSets {
        main {
            manifest.srcFile 'src/androidMain/AndroidManifest.xml'
            java.srcDirs = ['src/androidMain/kotlin']
            res.srcDirs = ['src/androidMain/res']
        }
        test {
            java.srcDirs = ['src/androidTest/kotlin']
            res.srcDirs = ['src/androidTest/res']
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

kotlin.sourceSets.all {
    languageSettings {
        enableLanguageFeature("InlineClasses")
        useExperimentalAnnotation 'kotlin.contracts.ExperimentalContracts'
        useExperimentalAnnotation 'kotlinx.coroutines.ExperimentalCoroutinesApi'
        useExperimentalAnnotation 'kotlinx.coroutines.ObsoleteCoroutinesApi'
        useExperimentalAnnotation 'kotlinx.coroutines.FlowPreview'
        useExperimentalAnnotation 'kotlin.time.ExperimentalTime'
    }
}

kotlin {

    android()
//    ios()

    sourceSets {
        commonMain.dependencies {
            api 'org.jetbrains.kotlin:kotlin-stdlib-common'

            implementation "org.jetbrains.kotlin:kotlin-stdlib-common:$kotlin_version"

            // COROUTINE
            implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core-common:$coroutine_version"

            // SERIALIZATION
            implementation "org.jetbrains.kotlinx:kotlinx-serialization-runtime-common:$serializer_version"

            // KTOR
            implementation "io.ktor:ktor-client-core:$ktor_version"
            implementation "io.ktor:ktor-client-json:$ktor_version"
            implementation "io.ktor:ktor-client-serialization:$ktor_version"
            implementation "io.ktor:ktor-client-logging:$ktor_version"
        }

        androidMain.dependencies {
            implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
            implementation "org.jetbrains.kotlin:kotlin-stdlib-common:$kotlin_version"

            // COROUTINE
            api "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutine_version"
            api "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutine_version"

            // SERIALIZATION
            implementation "org.jetbrains.kotlinx:kotlinx-serialization-runtime:$serializer_version"

            // KTOR
            implementation "io.ktor:ktor-client-okhttp:$ktor_version"
            implementation "io.ktor:ktor-client-json-jvm:$ktor_version"
            implementation "io.ktor:ktor-client-serialization-jvm:$ktor_version"
            implementation "io.ktor:ktor-client-logging-jvm:$ktor_version"

            // SQL Delight
            implementation "com.squareup.sqldelight:android-driver:$sqldelight_version"

            // ANDROIDX
            implementation "androidx.fragment:fragment-ktx:$android_fragment_version"
            implementation "androidx.lifecycle:lifecycle-viewmodel:$android_lifecycle_version"
            implementation "androidx.lifecycle:lifecycle-common-java8:$android_lifecycle_version"
            implementation "androidx.lifecycle:lifecycle-viewmodel-savedstate:$android_lifecycle_version"
            implementation "androidx.recyclerview:recyclerview:$android_recycler_version"
            implementation "androidx.constraintlayout:constraintlayout:$android_constraint_version"
            implementation "androidx.palette:palette:$android_palette_version"
            implementation "com.google.android.material:material:$android_material_version"

            implementation("com.github.bumptech.glide:glide:4.11.0") {
                exclude group: "com.android.support"
            }

            // DI
            api "org.koin:koin-core:$android_koin_version"
            api "org.koin:koin-android:$android_koin_version"
        }

        iosMain.dependencies {
            implementation "org.jetbrains.kotlin:kotlin-stdlib-common:$kotlin_version"

            // COROUTINE
            implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core-native:$coroutine_version"

            // SERIALIZATION
            implementation "org.jetbrains.kotlinx:kotlinx-serialization-runtime-native:$serializer_version"

            // KTOR
            implementation "io.ktor:ktor-client-ios:$ktor_version"
            implementation "io.ktor:ktor-client-core-native:$ktor_version"
            implementation "io.ktor:ktor-client-json-native:$ktor_version"
            implementation "io.ktor:ktor-client-serialization-native:$ktor_version"
            implementation "io.ktor:ktor-client-logging-native:$ktor_version"


            // SQL Delight
            implementation "com.squareup.sqldelight:ios-driver:$sqldelight_version"
        }

        commonTest {
            dependencies {
                implementation kotlin('test-common')
                implementation kotlin('test-annotations-common')
            }
        }

        androidTest {
            dependencies {
                implementation kotlin('test')
                implementation kotlin('test-junit')

                // actually not needed, see https://youtrack.jetbrains.com/issue/KT-31189
                implementation "org.jetbrains.kotlinx:kotlinx-serialization-runtime:$serializer_version"
            }
        }
    }
}


task packForXCode {
    def buildType = project.findProperty("kotlin.build.type") ?: "DEBUG"
    dependsOn "link${buildType.toLowerCase().capitalize()}FrameworkIos"
    //dependsOn "linkMainDebugFrameworkIOS"

    doLast {
        def srcFile = kotlin.targets.ios.binaries.getFramework(buildType).outputFile
        def targetDir = getProperty("configuration.build.dir")
        copy {
            from srcFile.parent
            into targetDir
            include 'shared.framework/**'
            include 'shared.framework.dSYM'
        }
    }
}


sqldelight {
    Database {
        packageName = "io.radio"
    }
}

// workaround for https://youtrack.jetbrains.com/issue/KT-27170
configurations {
    compileClasspath
}